MEMORY
{
  # iNES file format header.
  HEADER: start=$0000, size=$0010, type=ro, file=%O, fill=yes, fillval=$00;
  # Writable ram. Total size = $0800 = 2K.
  ZP:     start=$0000, size=$0100, type=rw;
  STACK:  start=$0100, size=$0100, type=rw;
  OAMRAM: start=$0200, size=$0100, type=rw;
  RAM:    start=$0300, size=$0500, type=rw;
  # Read-only prg rom. Segment size = $4000 = 16K.
  ROM:    start=$C000, size=$4000, type=ro, file=%O, fill=yes, fillval=$00;
  # Read-only chr data Segment size = $4000 = 16K.
  CHR:    start=$0000, size=$4000, type=ro, file=%O, fill=yes, fillval=$00;
}

SEGMENTS {
  INESHDR:  load=HEADER, type=ro;
  ZEROPAGE: load=ZP,     type=zp;
  STACK:    load=STACK,  type=bss, define=yes, optional=yes;
  OAMRAM:   load=OAMRAM, type=bss, define=yes, optional=yes;
  BSS:      load=RAM,    type=bss, define=yes;
  CODE:     load=ROM,    type=ro;
  VECTORS:  load=ROM,    type=ro,  start=$FFFA;
  CHRROM:   load=CHR,    type=ro;
}

FILES {
  %O: format=bin;
}
